
//
// This source file is part of appleseed.
// Visit http://appleseedhq.net/ for additional information and resources.
//
// This software is released under the MIT license.
//
// Copyright (c) 2016 Luis Barrancos, The appleseedhq Organization
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

// Ref: $MAYA_LOCATION/docs/Nodes/mountain.html

#include "appleseed/fractal/as_fractal_helpers.h"
#include "appleseed/math/as_math_helpers.h"
#include "appleseed/maya/as_maya_helpers.h"

shader as_maya_mountain
[[
    string maya_node_name = "mountain",
    string maya_classification = "texture/2d"
]]
(
    color in_snowColor = color(1)
    [[
        string maya_attribute_name = "snowColor",
        string maya_attribute_type = "vector",
        string label = "Snow Color"
    ]],
    color in_rockColor = color(0.262, 0.102, 0.0)
    [[
        string maya_attribute_name = "rockColor",
        string maya_attribute_type = "vector",
        string label = "Rock Color"
    ]],
    float in_amplitude = 1.0
    [[
        string maya_attribute_name = "amplitude",
        string maya_attribute_type = "float",
        string label = "Amplitude"
    ]],
    float in_snowRoughness = 0.4
    [[
        string maya_attribute_name = "snowRoughness",
        string maya_attribute_type = "float",
        string label = "Snow Roughness"
    ]],
    float in_rockRoughness = 0.707
    [[
        string maya_attribute_name = "rockRoughness",
        string maya_attribute_type = "float",
        string label = "Rock Roughness"
    ]],
    float in_boundary = 1.0
    [[
        string maya_attribute_name = "boundary",
        string maya_attribute_type = "float",
        string label = "Boundary"
    ]],
    float in_snowAltitude = 0.5
    [[
        string maya_attribute_name = "snowAltitude",
        string maya_attribute_type = "float",
        string label = "Snow Altitude"
    ]],
    float in_snowDropoff = 2.0
    [[
        string maya_attribute_name = "snowDropoff",
        string maya_attribute_type = "float",
        float slidermin = 0.0,
        float slidermax = 2.0,
        string label = "Snow Dropoff"
    ]],
    float in_snowSlope = 0.8
    [[
        string maya_attribute_name = "snowSlope",
        string maya_attribute_type = "float",
        float slidermin = 0.0,
        float slidermax = 3.0,
        string label = "Snow Slope"
    ]],
    float in_depthMax = 20.0
    [[
        string maya_attribute_name = "depthMax",
        string maya_attribute_type = "float",
        float slidermin = 0.0,
        float slidermax = 40.0,
        string label = "Depth Max"
    ]],
    
    MAYA_COLORBALANCE_PARAMETERS,
    MAYA_EFFECTS_PARAMETERS,
    MAYA_UV_PARAMETERS,

    output color out_outColor = color(0)
    [[
        string maya_attribute_name = "outColor",
        string maya_attribute_type = "vector",
        string label = "Output Color",
        string widget = "null"
    ]],
    output float out_outAlpha = 1.0
    [[
        string maya_attribute_name = "outAlpha",
        string maya_attribute_type = "float",
        string label = "Output Alpha",
        string widget = "null"
    ]]
)
{
    if (in_uvCoord[0] == OUTSIDE_UVFRAME || in_uvCoord[1] == OUTSIDE_UVFRAME)
    {
        out_outColor = in_defaultColor;
        out_outAlpha = maya_luminance(in_defaultColor);
    }
    else
    {
        float st[2] = {in_uvCoord[0], in_uvCoord[1]};

        float dst[2] = {
            in_filter *
                (in_uvFilterSize[0] == UNDEFINED_UVFILTER
                    ? filterwidth(st[0])
                    : in_uvFilterSize[0]),
            in_filter *
                (in_uvFilterSize[1] == UNDEFINED_UVFILTER
                    ? filterwidth(st[1])
                    : in_uvFilterSize[1])};

        dst[0] += in_filterOffset;
        dst[1] += in_filterOffset;

        // The following frequency value seems to match Maya's results roughly.

#ifndef MAYA_FRACTAL_FREQ
#define MAYA_FRACTAL_FREQ   14
#endif

        st[0] *= MAYA_FRACTAL_FREQ;
        st[1] *= MAYA_FRACTAL_FREQ;

        float filter_size = abs(max(dst[0], dst[1]));
        
        filter_size = clamp(
            MAYA_FRACTAL_FREQ * filter_size,
            MAYA_FRACTAL_FREQ * 1.0e-8,
            0.5);

        float iterate = min(in_depthMax, -log(filter_size) / M_LN2);

        int recursion_level = (int) trunc(iterate);

        float slope_x = 0, slope_y = 0, ofx = 0, ofy = 0, ch = 0, fx = 1;
        float amplitude = in_amplitude, amph = in_amplitude;
        
        for (int i = 0; i <= recursion_level; ++i)
        {
            if (i == recursion_level)
            {
                float diff = iterate - (float) recursion_level;

                amplitude *= diff;
                amph *= diff;
            }

            if (!amplitude)
            {
                break;
            }

            vector slope = (vector) noise(
                "perlin",
                st[0] * fx + ofx,
                st[1] * fx + ofy);

            ofx += 0.1;
            ofy += 0.03;

            slope_x += amplitude * slope[0];
            slope_y += amplitude * slope[1];

            fx *= 2;
            amplitude *= in_boundary;

            ch += amph * (float) noise(
                "perlin",
                st[0] * fx + ofx,
                st[1] * fx + ofy);

            amph *= in_rockRoughness;
        }

        float c = sqr(slope_x) + sqr(slope_y);
        float a = 0.5 * c;

        float ah = max(0.0, in_snowAltitude * in_snowDropoff), ratio;

        int snow_present = (ah + in_snowSlope <= a) ? 0 : 1;

        if (snow_present)
        {
            out_outColor = in_snowColor;
            ratio = in_snowRoughness;
        }
        else
        {
            out_outColor = in_rockColor;
            ratio = in_rockRoughness;
        }

        ofx = ofy = c = 0.0;
        fx = 1.0;

        for (int i = 0; i <= recursion_level; ++i)
        {
            if (i == recursion_level)
            {
                amplitude *= iterate - (float) recursion_level;
            }

            if (!amplitude)
            {
                break;
            }

            c += amplitude * (float) noise(
                "perlin",
                st[0] * fx + ofx,
                st[1] * fx + ofy);

            ofx += 0.1;
            ofy += 0.03;
            fx *= 2;

            amplitude *= ratio;
        }

        out_outAlpha = 0.5 * (1 + c);

        out_outColor = color(out_outAlpha);

        maya_colorBalance(
            in_colorGain,
            in_colorOffset,
            in_defaultColor,
            in_alphaGain,
            in_alphaOffset,
            in_invert,
            in_alphaIsLuminance,
            out_outColor,
            out_outAlpha);
    }
}
