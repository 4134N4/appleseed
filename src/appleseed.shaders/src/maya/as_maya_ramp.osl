
//
// This source file is part of appleseed.
// Visit http://appleseedhq.net/ for additional information and resources.
//
// This software is released under the MIT license.
//
// Copyright (c) 2016 Luis Barrancos, The appleseedhq Organization
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

// Ref: $MAYA_LOCATION/docs/Nodes/ramp.html

#include "appleseed/maya/as_maya_helpers.h"
#include "appleseed/maya/as_maya_ramp_helpers.h"

shader as_maya_ramp
[[
    string maya_node_name = "ramp",
    string maya_classification = "texture/2d"
]]
(
    int in_type = 0
    [[
        string maya_attribute_name = "type",
        string maya_attribute_type = "enum",
        string label = "Type",
        string widget = "mapper",
        string options = "U Ramp:0|V Ramp:1|Diagonal Ramp:2|Radial Ramp:3|Circular Ramp:4|Box Ramp:5|UV Ramp:6|Four Corner Ramp:7|Tartan Ramp:8",
        string page = "Ramp Attributes"
    ]],
    int in_interpolation = 1
    [[
        string maya_attribute_name = "interpolation",
        string maya_attribute_type = "enum",
        string label = "Interpolation",
        string widget = "mapper",
        string options = "None:0|Linear:1|Exponential Up:2|Exponential Down:3|Smooth:4|Bump:5|Spike:6",
        string page = "Ramp Attributes"
    ]],
    float in_position[] = {0}
    [[
        string maya_attribute_name = "colorEntryList",
        string maya_attribute_type = "float[]",
        string label = "Position",
        string page = "Ramp Attributes"
    ]],
    color in_color[] = {0}
    [[
        string maya_attribute_name = "colorEntryList",
        string maya_attribute_type = "color[]",
        string label = "Color",
        string page = "Ramp Attributes"
    ]],
    float in_uWave = 0.0
    [[
        string maya_attribute_name = "uWave",
        string maya_attribute_type = "float",
        string label = "U Wave",
        string page = "Ramp Attributes"
    ]],
    float in_vWave = 0.0
    [[
        string maya_attribute_name = "vWave",
        string maya_attribute_type = "float",
        string label = "V Wave",
        string page = "Ramp Attributes"
    ]],
    float in_noise = 0.0
    [[
        string maya_attribute_name = "noise",
        string maya_attribute_type = "float",
        string label = "Noise",
        string page = "Ramp Attributes"
    ]],
    float in_noiseFreq = 0.5
    [[
        string maya_attribute_name = "noiseFreq",
        string maya_attribute_type = "float",
        string label = "Noise Frequency",
        string page = "Ramp Attributes"
    ]],
    float in_hueNoise = 0.0
    [[
        string maya_attribute_name = "hueNoise",
        string maya_attribute_type = "float",
        string label = "Hue Noise",
        string page = "HSV Color Noise"
    ]],
    float in_satNoise = 0.0
    [[
        string maya_attribute_name = "satNoise",
        string maya_attribute_type = "float",
        string label = "Saturation Noise",
        string page = "HSV Color Noise"
    ]],
    float in_valNoise = 0.0
    [[
        string maya_attribute_name = "valNoise",
        string maya_attribute_type = "float",
        string label = "Value Noise",
        string page = "HSV Color Noise"
    ]],
    float in_hueNoiseFreq = 0.5
    [[
        string maya_attribute_name = "hueNoiseFreq",
        string maya_attribute_type = "float",
        string label = "Hue Noise Frequency",
        string page = "HSV Color Noise"
    ]],
    float in_satNoiseFreq = 0.5
    [[
        string maya_attribute_name = "satNoiseFreq",
        string maya_attribute_type = "float",
        string label = "Saturation Noise Frequency",
        string page = "HSV Color Noise"
    ]],
    float in_valNoiseFreq = 0.5
    [[
        string maya_attribute_name = "valNoiseFreq",
        string maya_attribute_type = "float",
        string label = "Value Noise Frequency",
        string page = "HSV Color Noise"
    ]],

    MAYA_COLORBALANCE_PARAMETERS,
    MAYA_EFFECTS_PARAMETERS,
    MAYA_UV_PARAMETERS,

    output color out_outColor = color(0)
    [[
        string maya_attribute_name = "outColor",
        string maya_attribute_type = "vector",
        string label = "Output Color",
        string widget = "null"
    ]],
    output float out_outAlpha = 0.0
    [[
        string maya_attribute_name = "outAlpha",
        string maya_attribute_type = "float",
        string label = "Output Alpha",
        string widget = "null"
    ]]
)
{
    if (in_uvCoord[0] == OUTSIDE_UVFRAME || in_uvCoord[1] == OUTSIDE_UVFRAME)
    {
        out_outColor = in_defaultColor;
        out_outAlpha = maya_luminance(in_defaultColor);
    }
    else
    {
        float st[2] = {in_uvCoord[0], in_uvCoord[1]}, noise_val = 0;

        if (in_noise)
        {
            noise_val = in_noise * noise(
                "perlin",
                in_noiseFreq * st[0] * 15, // approximates Maya results
                in_noiseFreq * st[1] * 15);
        }

        int interpolation[1] = {in_interpolation}; // share with ramp shader

        st[0] = mod(st[0] + noise_val, 1);
        st[1] = mod(st[1] + noise_val, 1);

        if (in_type == 0)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                st[1]);
        }
        else if (in_type == 1)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                st[0]);
        }
        else if (in_type == 2)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                0.5 * (st[0] + st[1]));
        }
        else if (in_type == 3)
        {
            float value = atan2(st[0] - 0.5, st[1] - 0.5) / M_2PI + 0.5;
#ifdef DEBUG
            string shadername = "";
            getattribute("shader:shadername", shadername);

            if (isnan(value) || isinf(value))
            {
                warning("[WARNING]: NaN or Inf in %s, %s:%i\n",
                        shadername, __FILE__, __LINE__);
#endif
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                value);
        }
        else if (in_type == 4)
        {
            float value = hypot(
                M_SQRT2 * (st[0] - 0.5),
                M_SQRT2 * (st[1] - 0.5));

            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                value);
        }
        else if (in_type == 5)
        {
            float abs_st[2] = {abs(st[0]), abs(st[1])};

            float value = (abs_st[0] > abs_st[1])
                ? 2 * abs_st[0]
                : 2 * abs_st[1];

            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                value);
        }
        else if (in_type == 6)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                2 * abs(st[0] - 0.5));

            out_outColor *= color_ramp(
                in_color,
                in_position,
                interpolation,
                2 * abs(st[1] - 0.5));
        }
        else if (in_type == 7)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                (1 - st[0]) * (1 - st[1]));

            int len = arraylength(in_position);

            if (len > 1)
            {
                out_outColor += color_ramp(
                    in_color,
                    in_position,
                    interpolation,
                    st[0] * (1 - st[1]));
            }
            if (len > 2)
            {
                out_outColor += color_ramp(
                    in_color,
                    in_position,
                    interpolation,
                    (1 - st[0]) * st[1]);
            }
            if (len > 3)
            {
                out_outColor += color_ramp(
                    in_color,
                    in_position,
                    interpolation,
                    st[0] * st[1]);
            }
        }
        else if (in_type == 8)
        {
            out_outColor = color_ramp(
                in_color,
                in_position,
                interpolation,
                2 * abs(st[1] - 0.5));

            out_outColor += color_ramp(
                in_color,
                in_position,
                interpolation,
                2 * abs(st[0] - 0.5));

            out_outColor *= 0.5;
        }

        maya_colorBalance(
            in_colorGain,
            in_colorOffset,
            in_defaultColor,
            in_alphaGain,
            in_alphaOffset,
            in_invert,
            in_alphaIsLuminance,
            out_outColor,
            out_outAlpha);
    }
}

